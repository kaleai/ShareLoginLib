apply plugin: 'com.android.application'

Properties props = new Properties()
File f = file(rootProject.file("signi.properties"))
if (!f.exists()) {
    f = file(rootProject.file("gradle.properties"))
}
props.load(new FileInputStream(f))

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    defaultConfig {
        applicationId "com"
        minSdkVersion 18
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        manifestPlaceholders = [tencentAuthId: props['TENCENT_AUTHID']]
    }

    signingConfigs {
        release {
            storeFile file(props['STORE_FILE_PATH'])
            storePassword props['STORE_PASSWORD']
            keyAlias props['KEY_ALIAS']
            keyPassword props['KEY_PASSWORD']
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.liulishuo.debug'
        }
        release {
            minifyEnabled false
            shrinkResources false // 是否去除无效的资源文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            applicationIdSuffix '.liulishuo.release'
        }
        signed.initWith(buildTypes.release)
        signed {
            applicationIdSuffix props['PACKAGE_NAME_SUFFIX']
        }
    }
}

dependencies {
    compile project(':share')

    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.android.support:appcompat-v7:24.2.1'

    // Dependencies for local unit tests
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-junit:2.0.0.0'

    // Android Testing Support Library's runner and rules
    androidTestCompile 'com.android.support:support-annotations:24.2.1'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'

    // UiAutomator
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'

    // Espresso UI Testing
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}